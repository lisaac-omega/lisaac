Header
  + name    := Expanded REAL_16_16;
  /*signed 16.16 fixed float.*/
  - type    := `signed int`;
  - default := (0.to_raw_real_16_16);
  - export  := REAL_32, REAL_64;

Insert
  - parent_signed_fixed_real:SIGNED_FIXED_REAL := SIGNED_FIXED_REAL

SELF

  - shift_bits:INTEGER <- 16

Public

  - round:INTEGER <-
  // Rounded integral value.
  ( + result:INTEGER
    result := to_raw_integer
    ((result & 8000h) != 0).if {
      result := (result >> 16) + 1
    } else {
      result := result >> 16
    }
    result
  )

  // Range.

  - minimum:INTEGER_64  <- INTEGER_16.minimum

  - maximum:UINTEGER_64 <- INTEGER_16.maximum

  // Convertion format with test.

  - to_real_16_16:REAL_16_16 <- Self

  // Convertion

  - append_in b:STRING_BUFFER <-
  // Append in the `b' the equivalent of `to_string'. No new STRING_BUFFER
  // creation during the process.
  ( + val:INTEGER
    + old_count,new_count:INTEGER
    ? {b!=NULL}
    val := to_raw_integer
    (val < 0).if {
      val := - val
      b.extend '-'
    }
    (val >> 16).append_in b
    b.extend '.'
    old_count := b.count
    //     1/65536 = 0.00390625 => 8 digit.
    (((val & 0FFFFh).to_uinteger_64 * 100000000)>>16).to_integer.append_in b
    new_count:=old_count+8
    {b.count!=new_count}.while_do {
      b.insert '0' to old_count
    }
  )









