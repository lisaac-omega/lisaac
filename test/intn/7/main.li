Header
  + name := MAIN;
  
Private
  - values:ARRAY INTN
  
  - valook: ARRAY STRING_ALIAS
  
  - valhex: ARRAY STRING_ALIAS
  
  - operations:STRING_ALIAS := "***+***++**++**++**++**++****+++++++++++"
  
Public
  - main <-
  ( + intn:INTN
    + a,b,c:INTN
    + j:INTEGER
    + op:CHARACTER
    values := ARRAY INTN .create_with_capacity 1024
    valook := ARRAY STRING_ALIAS .create_with_capacity 1024
    valhex := ARRAY STRING_ALIAS .create_with_capacity 1024
    intn := 1
    values_add intn
    intn := 3
    values_add intn
    0.to (operations.upper) do { i:INTEGER
      a := value_at (values.upper - 1)
      b := value_at (values.upper)
      op := operations.at i
      (op = '+').if {
        values_add (a + b)
      }.elseif {op = '-'} then {
        values_add (a - b)
      }.elseif {op = '*'} then {
        values_add (a * b)
      }.elseif {op = '/'} then {
        values_add (a / b)
      } else {
        TEST.check FALSE
      }
      "========".println; a.println; op.println; b.println ; " = ".println; values.last.println
    }
    j := values.upper
    (operations.upper).downto 0 do { i:INTEGER
      c := value_at (values.upper)
      b := value_at (values.upper - 1)
      a := value_at (values.upper - 2)
      op := operations.at i
      (op = '+').if {
        op := '-'; intn := c - b
      }.elseif {op = '-'} then {
        op := '+'; intn := c + b
      }.elseif {op = '*'} then {
        op := '/'; intn := c / b
      } else {
        TEST.check FALSE
      }
      "--------".println; c.print; op.println; b.println ; " = ".print; intn.println
      TEST.check (intn == a)
      TEST.check (intn.to_string) string (valook.at (values.upper - 2))
      TEST.check (intn.to_hexadecimal) string (valhex.at (values.upper - 2))
      remove_last
    }
    TEST.check (values.count = 2)
    TEST.check (valook.count = 2)
    TEST.check (valhex.count = 2)
  )
  
  - values_add z:INTN <-
  (
    TEST.check (values.upper = valook.upper)
    TEST.check (values.upper = valhex.upper)
    values.add_last z
    valook.add_last (z.to_string)
    valhex.add_last (z.to_hexadecimal)
  )
  
  - value_at idx:INTEGER :INTN <-
  ( + result:INTN
    TEST.check (values.upper = valook.upper)
    TEST.check (values.upper = valhex.upper)
    result := values.at idx
    TEST.check (result.to_hexadecimal) string (valhex.at idx)
    TEST.check (result.to_string) string (valook.at idx)
    result
  )
  
  - remove_last <-
  (
    TEST.check (values.upper = valook.upper)
    TEST.check (values.upper = valhex.upper)
    values.remove_last
    valook.remove_last
    valhex.remove_last
  )
  
